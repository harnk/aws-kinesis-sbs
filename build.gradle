import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        versions = [
                kotlin    : '1.3.30',
                kinesis   : '1.9.3',
                awsSdk    : '1.11.438'
        ]
    }

    ext["mockito.version"] = "2.13.0"
    ext["kotlin.version"] = ext.versions.kotlin

    ext.allRepositories = {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    repositories(allRepositories)

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${versions.kotlin}"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2"
    }
}

plugins {
    id 'org.springframework.boot' version "2.1.0.RELEASE" apply false
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()

        force "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}"
        force 'org.jetbrains:annotations:15.0'
    }
}

allprojects {
    repositories(allRepositories)
}
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: "io.spring.dependency-management"

apply from: "build-scripts/integration-tests.gradle"
group = 'de.bringmeister'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

configurations {
    all*.exclude module: 'commons-logging'
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    compile "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-configuration-processor"
    compile "com.amazonaws:aws-java-sdk-sts:${versions.awsSdk}"
    compile "com.amazonaws:amazon-kinesis-client:${versions.kinesis}"
    compile "org.slf4j:slf4j-api"
    compile "javax.validation:validation-api"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin"

    compileOnly "org.springframework.boot:spring-boot-starter-actuator"

    testCompile "org.springframework.boot:spring-boot-starter-actuator"
    testCompile "org.hibernate:hibernate-validator:6.0.11.Final"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile "org.testcontainers:testcontainers:1.10.1"
    testCompile("org.awaitility:awaitility-kotlin:3.1.6") {
        exclude group: "org.jetbrains.kotlin"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "**/WorkerFactory**",
                "**/local/**"
            ])
        })
    }
}

coveralls {
    // Add kotlin source dir, since the coveralls plugin needs the sources and
    // does not pick it up automatically.
    // See https://github.com/kt3k/coveralls-gradle-plugin/issues/63
    sourceDirs += ['src/main/kotlin']
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
